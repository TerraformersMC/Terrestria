archivesBaseName = "terrestria-common"
version = formatVersion(project, "4.0.0")

loom {
	runs {
		datagen {
			server()

			name = "Data Generation"
			runDir = "build/datagen"

			vmArg("-Dfabric-api.datagen")
			vmArg("-Dfabric-api.datagen.output-dir=${file("src/main/generated")}")
		}
	}
}

// This is a sad, janky thing that Minecraft makes me do.
tasks.register('copyCommon', Copy) {
	from "src/main/generated/data/terrestria-common"
	into "src/main/generated/data/terrestria"
}
tasks.register('removeCommon', Delete) {
	delete "src/main/generated/data/terrestria-common"
}
copyCommon.dependsOn(runDatagen)
removeCommon.dependsOn(copyCommon)
assemble.dependsOn(removeCommon)

sourceSets {
	main {
		resources {
			srcDirs += [
					"src/main/generated"
			]
		}
	}
}
